# thrive.test -=- Low-level tests for the Tcl binding to Thrive

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}

load ./libthrive2.23[info sharedlibext]
package require thrive

set tscript {
  0 exit exec -1 syst #We

  immed 1 Wi Wl b!
  noop exit
  ]] -1 (to) Wt exit
  : 2 (to) Wt 0 syst ]] exit
  , Wc ! 1 +to Wc exit
  ; (lit) exit , 0 (to) Wt exit

  [[ immed

  : (tok) #-e 0 syst ;
  : .p #v- 3 syst ;

  : constant (tok) ! ;

  Ww 100 box constant infv
  Ww 101 box constant exef
  Ww 102 box constant argv

  : hello #-s infv .p " #" .p argv tlen b2i .p ": " .p argv .p "\n" .p 'OK ;
}

#------------------------------------------------------------------------------

proc readfile {fn} {
  set fd [open $fn]
  fconfigure $fd -translation binary
  set data [read $fd]
  close $fd
  return $data
}

# a slightly more elaborate run loop, used to call existing words
proc tcall {cmd {fmt ""} args} {
  global T
  eval [linsert $args 0 $T push $fmt]
  set r [$T run [$T find $cmd]]
  while {$r != -1} {
    switch -- $r {
      0 -
      1 { set t [$T pop]; $T push $t [eval [$T pop]] }
      2 { error "token?" }
      3 { puts -nonewline [$T pop] }
      4 { $T push S [readfile [$T pop]] }
      5 { $T push I [clock clicks] }
      6 { if {[catch { $T push S $env([$T pop]) }]} { $T push n - } }
      default { error $r }
    }
    set r [$T run]
  }
  $T pop
}

test thrive-1.0 {create a workspace} {
  set T [thrive]
} {::thrive_1}

test thrive-1.1 {second workspace} {
  thrive
} {::thrive_2}

test thrive-1.2 {push exe & arg vec} {
  $T push S(S) [info nameofexe] {test arg2 arg3}
} {}

test thrive-1.3 {load bootstrap} {
  $T load $tscript
} {}

test thrive-1.4 {init message} -body {
  tcall hello
} -match glob -result OK -output \
    "Tcl 223 {??? ?? 200? ??:??:??} ? #3: test arg2 arg3\n"

test thrive-1.5 {basic call} {
  tcall + II 1 2
} {3}

test thrive-1.6 {vm information} -body {
  tcall infv
} -match glob -result {Tcl 223 {??? ?? 200? ??:??:??} [48]}

test thrive-1.7 {arg list} {
  tcall argv
} {test arg2 arg3}

test thrive-1.8 {define and run a new word three times} {
  $T load { : t16 10 * ; }
  list [tcall t16 I 123] [tcall t16 I 234] [tcall t16 I 345]
} {1230 2340 3450}

::tcltest::cleanupTests
