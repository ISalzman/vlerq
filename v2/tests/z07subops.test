#!/usr/bin/tclsh
# z07subops.test -=- test suite for the ratcl view operators for subviews
# %renumber<^test >%

set uses {thrill ratcl}
source [file join [file dirname [info script]] initests.tcl]

test 0 {setup test data} {
  set R [vdef A B C   {a b c d a f c b d}]
  set S [vdef D E F   {b g a d a f}]
  set T [vdef A B C D {a b c d a b e f b c e f e d c d e d e f a b d e}]
  set U [vdef C D E   {c d e c d f d e f}]
  set V [vdef C D     {c d e f}]
  set W [vdef A B C   {1 2 3 a b c}]
  set X [vdef D E:I F {a 100 aa b 20 bb c 3 cc}]
  set Y [vdef D       {y}]
  set Z [vdef E       {}]
  return
} {}

test 1 {group} {
  set v [view $T group A B G]
  list [view $v info names] [view $v info types] [view $v project A B | get]
} {{A B G} {S S V} {a b b c e d}}

test 2 {group subview structure} {
  set v [view $T group A B G | get 0 G]
  list [view $v info names] [view $v info types] [view $v get]
} {{C D} {S S} {c d e f d e}}

test 3 {group subview contents} {
  set v [view $T group A B G]
  list [view $v get 0 G | get] \
	[view $v get 1 G | get] \
	 [view $v get 2 G | get]
} {{c d e f d e} {e f} {c d e f}}

test 4 {no cols} {
  set v [view $V group G]
  list [view $v get #] [view $v get 0 G #] [view $v get 0 G | get]
} {1 2 {c d e f}}

test 5 {group subview counts} {
  view $T group A B G | counts G
} {3 1 2}

test 6 {divide} {
  set v [view $T divide $V] 
  list [view $v info names] [view $v info types] [view $v get]
} {{A B} {S S} {a b e d}}

test 7 {nspread} {
  view $R nspread {3 0 1} | get
} {a b c a b c a b c c b d}

test 8 {subcat} {
  view $T group A B G | subcat G | get
} {c d e f d e e f c d e f}

test 9 {ungroup} {
  view $T group A B G | ungroup G | get
} {a b c d a b e f a b d e b c e f e d c d e d e f}

test 10 {join without subviews} {
  set v [view $T join $U J | mapcols -omit J]
  list [view $v info names] [view $v info types] [view $v get]
} {{A B C D} {S S S S} {a b c d a b e f b c e f e d c d e d e f a b d e}}

test 11 {join and ungroup} {
  set v [view $T join $U J | ungroup J]
  list [view $v info names] [view $v info types] [view $v get]
} {{A B C D E} {S S S S S} {a b c d e a b c d f e d c d e e d c d f a b d e f}}

test 12 {join_i} {
  set v [view $T join_i $U]
  list [view $v info names] [view $v info types] [view $v get]
} {{A B C D E} {S S S S S} {a b c d e a b c d f e d c d e e d c d f a b d e f}}

test 13 {join0} {
  set v [view $T join0 $U]
  list [view $v info names] [view $v info types] [view $v get]
} {{A B C D} {S S S S} {a b e f b c e f e d e f}}

test 14 {multiple join columns} {
  set v [vdef A B C:I { x r -1
			x d  0 }]
  set w [vdef B C:I D E:I { r -1 aa  5
			    d  0 ff  4
			    d  0 eee 3 }]
  view $v join $w J | pair [vdef F {f f2}] | ungroup J | dump
} { \
  A  B  C   F   D    E
  -  -  --  --  ---  -
  x  r  -1  f   aa   5
  x  d   0  f2  ff   4
  x  d   0  f2  eee  3}

test 15 {no common columns} {
  set v [view $W join $S J | ungroup J]
  list [view $v info names] [view $v info types] [view $v get]
} {{A B C D E F} {S S S S S S}\
	{1 2 3 b g a 1 2 3 d a f a b c b g a a b c d a f}}

test 16 {join_l} {
  set v [view $T join_l $U]
  list [view $v info names] [view $v info types] [view $v get]
} {{A B C D E} {S S S S S} {a b c d e a b c d f a b e f {} b c e f {}\
			    e d c d e e d c d f e d e f {} a b d e f}}

test 17 {subview} {
  set v [vdef X:V [list [vdef A B C {a1 b1 c1 d1 e1 f1}]]]
  list [view $v info names] [view $v info types] [view $v get 0 0 #]
} {X V 2}

test 18 {subviews in vdef} {
  set v1 [vdef A B C {a1 b1 c1 d1 e1 f1}]
  set v2 [vdef A B C {a2 b2 c2 d2 e2 f2}]
  set v3 [vdef A B C {a3 b3 c3 d3 e3 f1}]
  set v [vdef X:V [list $v1 $v2 $v3]]
  list [view $v info names] [view $v info types] [view $v ungroup X | get]
} {X V {a1 b1 c1 d1 e1 f1 a2 b2 c2 d2 e2 f2 a3 b3 c3 d3 e3 f1}}

test 19 {subview construction} {
  set v [vdef A {B {C D}} E {a1 {} e1 a2 {c21 d21} e2 a3 {c31 d31 c32 d32} e3}]
  list [view $v info names] [view $v info types] [view $v get #]
} {{A B E} {S V S} 3}

test 20 {subview contents} {
  view $v ungroup B | get
} {a2 e2 c21 d21 a3 e3 c31 d31 a3 e3 c32 d32}

test 21 {empty subview construction} {
  set v [vdef A {B {C D}} E {}]
  list [view $v info names] [view $v info types] [view $v get #]
} {{A B E} {S V S} 0}

test 22 {ungroup} {
  set w [vdef a b {}]
  set w [view $w append [vdef a b {1 2 1 3 1 4 2 5}]]
  view $w group a j | ungroup j | get
} {1 2 1 3 1 4 2 5}

test 23 {ungroup mutated subview} {
  set w [vdef a {j b} {1 {} 2 {}}]
  view $w get 0 j | append [vdef b {2 3 4}]
  view $w get 1 j | append [vdef b 5]
  view $w ungroup j | get
} {1 2 1 3 1 4 2 5}

test 24 {data stack is empty} {
  vfinish
} {}

cleanupTests

# vim: set ft=tcl :
