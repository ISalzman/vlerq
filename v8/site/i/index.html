[stop?]
<br />

%#[tests]
[modules]
[sources]
%#[docs]
[channels]
[requests]
[timers]
[hooks]
[sysinfo]

[switch [REQ query] p pkginfo t tminfo v vsninfo]


<%DEF .title %>
Info Area


<%DEF stop? %>
% if {[REQ query] eq "a=stop"} {
%   set ::Rig::exit 0
    <h1>Server stopped.</h1>
% } else {
    <form>
      <input type=hidden name=a value=stop>
      <input type=submit value='Stop server'>
    </form>
% }


<%DEF tests %>
<b>Tests</b>
<blockquote>
% foreach f [lsort -dict [glob -dir [Rig home]/site/t -tails *.html]] {
%   if {$f ne "index.html"} {
      [url [file root $f] ../t/$f]
%   }
% }
</blockquote>


<%DEF docs %>
<b>Docs</b>
<blockquote>
% foreach f [lsort -dict [glob -nocomplain -dir [Rig home]/docs -tails *.txt]] {
    [url [file root $f] d/$f]
% }
</blockquote>


<%DEF sources %>
<b>Sources</b>
<blockquote>
% foreach f [lsort -dict [glob -dir $::Rig::v::rigdir -tails *.tcl]] {
    [url [file root $f] f/[file tail $::Rig::v::rigdir]/$f]
% }
</blockquote>


<%DEF channels %>
<b>Channels</b>
<blockquote>
  [lsort -dict [chan names]]
</blockquote>


<%DEF modules %>
<b>Modules</b>
<blockquote>
% foreach ns [lsort -dict [namespace children ::]] {
%   if {[namespace ensemble exists $ns] && [info exists ${ns}::~]} {
%     set m [namespace tail $ns]
      [url $m m/[string map {/ %2f} $m]/]
%   }  
% }
  ([url other m/])
</blockquote>


<%DEF requests %>
<b>Requests</b>
<blockquote>
  <table cellspacing='0'>
%   foreach ns [namespace children ::jcw/Httpd::v] {
      [trow [namespace tail $ns] [set ${ns}::request(url)]]
%   }
  </table>
</blockquote>


<%DEF timers %>
<b>Timers</b>
<blockquote>
  <table cellspacing='0'>
%   foreach t [lsort -dict [after info]] {
%     if {[lassign [after info $t] s] eq "timer"} {
        [trow $t $s]
%     }
%   }
  </table>
</blockquote>


<%DEF sysinfo %>
<b>System</b>
<blockquote>
  <table cellspacing='0'>
    [trow Tcl "[info patchlevel] ([archinfo])"]
    [trow exe [info nameofexe]]
    [trow pwd [pwd]/]
    [trow [url home f/] [Rig home]]
    [trow loaded [lsort -dict [extensions]]]
    [trow other "[url packages ?p] [url tm::path ?t] [url versions ?v]"]
  </table>
</blockquote>


<%DEF pkginfo %>
<b>Packages</b>
<blockquote>
  <table cellspacing='0'>
%   foreach p [lsort -dict [package names]] {
%     set v [lsort -dict [package versions $p]]
%			set l ""
%     if {![catch { package present $p }]} {
%       set r [package require $p]
%       if {$v eq ""} { set v $r }
%				set l "($r)"
%     }
      [trow $p $v $l]
%   }
  </table>
</blockquote>


<%DEF vsninfo %>
<b>Versions</b>
<blockquote>
  <table cellspacing='0'>
%   foreach ns [lsort -dict [allnamespaces]] {
%     foreach v [lsort -dict [info vars ${ns}::~*]] {
%       if {[string match *Id:* [set $v]]} {
%       set mod [regsub :* $ns {}]
%       if {![regexp {(.*/)(.*)} $mod - who mod]} { set who "" }
%       set svn [lrange [set $v] 1 end-1]
          [trow $who $mod [regsub {.*~} $v {}] {*}$svn]
%       }
%     }
%   }
  </table>
</blockquote>


<%DEF tminfo %>
<b>tm::path</b>
<blockquote>
%   foreach p [lsort -dict [tcl::tm::path list]] {
      $p <br/>
%   }
</blockquote>


<%DEF hooks %>
<b>Hooks</b>
<blockquote>
  <table cellspacing='0'>
%   foreach e [lsort -dict [array names ::Rig::v::hooks]] {
% 		foreach t $Rig::v::hooks($e) {
%				lassign $t prio x
% 			lassign $x - - ns cmd
	      [trow $e $prio $ns $cmd]
% 		}
%   }
  </table>
</blockquote>


<%
  proc url {text {url ""}} {
    return "<a href='$url'>$text</a>"
  }
  proc trow {a args} {
		set b [join $args {</td><td valign=top>}]
    return "<tr><td align=right valign=top>$a</td>
								<td valign=top>$b</td></tr>"
  }
  proc extensions {} {
    set r {}
    foreach e [info loaded] {
      lassign $e path name
      if {$path ne ""} {
        append name ([file tail $path])
      }
      lappend r $name
    }
    return $r
  }
  proc archinfo {} {
    upvar #0 tcl_platform p
    return "$p(os) $p(osVersion) - $p(machine)"
  }
  proc allnamespaces {{ns ::}} {
    set i -1
    while {[incr i] < [llength $ns]} {
      lappend ns {*}[namespace children [lindex $ns $i]]
    }
    return $ns
  }
%>
